//noinspection BnfResolveForFile
{
  generate=[java="8" names="long"]

  parserClass="org.ice1000.tt.psi.minitt.MiniTTParser"

  psiClassPrefix="MiniTT"
  psiImplClassSuffix="Impl"
  psiPackage="org.ice1000.tt.psi.minitt"
  psiImplPackage="org.ice1000.tt.psi.minitt.impl"

  elementTypeHolderClass="org.ice1000.tt.psi.minitt.MiniTTTypes"
  elementTypeClass="org.ice1000.tt.psi.minitt.MiniTTElementType"
  tokenTypeClass="org.ice1000.tt.psi.minitt.MiniTTTokenType"
}

file ::= expression

expression ::=
   declaration_expression
 | const_expression
 | merge_sum
 | application
 | function_type
 | pair_type
 | first
 | second
 | pair
 | atom

atom ::=
   universe
 | sum
 | constructor
 | variable
 | split
 | one
 | lift
 | unit
 | pi_type
 | sigma_type
 | lambda_expression
 | LEFT_PAREN expression RIGHT_PAREN
 { extends=expression }

// Syntactic sugars: short-hand for unit patterns
function_type ::= atom ARROW expression { extends=expression }
pair_type ::= atom MUL expression { extends=expression }

// Higher-level expressions
first ::= atom DOT_ONE { extends=expression }
lift ::= UP atom { extends=expression }
second ::= atom DOT_TWO { extends=expression }
application ::= atom expression { extends=expression }
pair ::= atom COMMA expression { pin=2 extends=expression }
merge_sum ::= atom CONCAT expression { pin=2 extends=expression }
variable ::= IDENTIFIER {
	extends=expression
	mixin='org.ice1000.tt.psi.minitt.MiniTTVariableMixin'
}

// Prefixed parameters
prefix_parameter ::= LEFT_PAREN typed_pattern RIGHT_PAREN

// Patterns
pattern ::= pair_pattern | atom_pattern {
	implements=['org.ice1000.tt.psi.IPattern<MiniTTVariable>' 'com.intellij.psi.PsiNameIdentifierOwner']
	mixin='org.ice1000.tt.psi.minitt.MiniTTGeneralPattern'
}
atom_pattern ::= variable | META_VAR | LEFT_PAREN pattern RIGHT_PAREN {
	extends=pattern
	mixin='org.ice1000.tt.psi.minitt.MiniTTAtomPatternMixin'
	implements=['com.intellij.psi.PsiNameIdentifierOwner']
}
pair_pattern ::= atom_pattern COMMA pattern {
	pin=2
	extends=pattern
	mixin='org.ice1000.tt.psi.minitt.MiniTTPairPatternMixin'
	implements=['com.intellij.psi.PsiNameIdentifierOwner']
}

// Extracted helpers
typed_abstraction ::= typed_pattern DOT expression {
	pin=2
	mixin='org.ice1000.tt.psi.minitt.MiniTTTypedAbstractionMixin'
}

branches ::= LEFT_BRACE (constructor (SEP constructor)*)? RIGHT_BRACE { pin=1 }
choices ::= LEFT_BRACE (pattern_match (SEP pattern_match)*)? RIGHT_BRACE { pin=1 }
typed_pattern ::= pattern COLON expression {
	pin=2
	mixin='org.ice1000.tt.psi.minitt.MiniTTTypedPatternGeneratedMixin'
}

// Atomic expressions
pi_type ::= PI typed_abstraction {
	pin=1
	extends=expression
	mixin='org.ice1000.tt.psi.TypedAbstractionOwnerMixin<MiniTTTypedAbstraction>'
	implements=['org.ice1000.tt.psi.TypedAbstractionOwner<MiniTTTypedAbstraction>']
}

lambda_expression ::= LAMBDA pattern DOT expression {
	pin=1
	extends=expression
	mixin='org.ice1000.tt.psi.minitt.MiniTTLambdaExpressionGeneratedMixin'
}

constructor ::= CONSTRUCTOR_NAME expression? { pin=1 extends=expression }
pattern_match ::=
 CONSTRUCTOR_NAME
 pattern?
 DOUBLE_ARROW
 expression { pin=1 }

sigma_type ::= SIGMA typed_abstraction {
	pin=1
	extends=expression
	mixin='org.ice1000.tt.psi.TypedAbstractionOwnerMixin<MiniTTTypedAbstraction>'
	implements=['org.ice1000.tt.psi.TypedAbstractionOwner<MiniTTTypedAbstraction>']
}

split ::= SPLIT_KEYWORD choices { pin=1 extends=expression }
sum ::= SUM_KEYWORD branches { pin=1 extends=expression }

one ::= ONE_KEYWORD { extends=expression }
unit ::= UNIT_KEYWORD { extends=expression }
universe ::= TYPE_UNIVERSE { extends=expression }

// Declaration
const_declaration ::=
 CONST_KEYWORD
 pattern
 EQ expression
 SEMICOLON {
	pin=1
	mixin='org.ice1000.tt.psi.minitt.MiniTTConstDeclarationGeneratedMixin'
}

const_expression ::= const_declaration expression? {
	extends=expression
	mixin='org.ice1000.tt.psi.minitt.MiniTTConstExpressionMixin'
}

declaration ::=
 (LET_KEYWORD | REC_KEYWORD)
 pattern
 prefix_parameter*
 COLON expression
 EQ expression
 SEMICOLON {
	pin=1
	mixin='org.ice1000.tt.psi.minitt.MiniTTDeclarationMixin'
}

declaration_expression ::= declaration expression? {
	extends=expression
	mixin='org.ice1000.tt.psi.minitt.MiniTTDeclarationExpressionMixin'
}
